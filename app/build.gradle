apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.1'
    //boolean to easily change from build general to individual processor apk
    def isGeneralApk = false
    defaultConfig {
        applicationId "com.dt3264.deezloader"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 33
        versionName "2.0.9"
        archivesBaseName = "DeezLoader-$versionName"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }
        if (isGeneralApk) {
            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            //include works separately of ndk{} and universalApk
            if (!isGeneralApk) include "armeabi-v7a", "x86"
            if (isGeneralApk) universalApk true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    // If you want Gradle to package prebuilt native libraries
    // with your APK, modify the default source set configuration
    // to include the directory of your prebuilt .so files as follows.
    sourceSets {
        main {
            jniLibs.srcDirs 'libnode/bin/'
        }
    }
    applicationVariants.all { variant ->
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assemble.doLast {
                (new File(variant.mappingFile.parent, "$archivesBaseName-$variant.baseName-mapping.txt")).delete()
                variant.mappingFile.renameTo(variant.mappingFile.parent +
                        "/$archivesBaseName-$variant.baseName-mapping.txt")
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations' })

    implementation 'com.android.support:appcompat-v7:28.0.0-beta01'
    implementation 'com.android.support:cardview-v7:28.0.0-beta01'
    implementation 'com.android.support:design:28.0.0-beta01'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json' }

    debugImplementation('com.github.ajitsing:sherlock:1.0.4@aar') {
        transitive = true }

    releaseImplementation 'com.github.ajitsing:sherlock-no-op:1.0.4@aar'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.github.vic797:prowebview:2.2.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    testImplementation 'junit:junit:4.12'
}
